apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// 扩展定义
ext {
    // 启动类
    MainClass = 'com.changhong.sei.auth.AuthApplication'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation project(':sei-auth-api')

    compile(
            "org.springframework.boot:spring-boot-configuration-processor",
            "org.springframework.boot:spring-boot-starter-websocket",
            // thymeleaf
            "org.springframework.boot:spring-boot-starter-thymeleaf",

            // 直接引用消息通知包
            "com.changhong.sei:sei-notify-sdk:$sei_version",
            "com.changhong.sei:sei-monitor-starter:$sei_version",

            "mysql:mysql-connector-java:$mysqlVersion",
            "com.changhong.sei:sei-cloud-starter:$sei_version"
    )

    // 获取用户浏览器、操作系统等信息
    compile 'eu.bitwalker:UserAgentUtils:1.21'


    //长虹单点登录组件包
    compile group: 'com.auth0', name: 'java-jwt', version: '2.3.0'

    testImplementation(
            "com.changhong.sei:sei-test-starter:$sei_version"
//            "org.springframework.boot:spring-boot-starter-test"
    )
}

// 处理资源配置文件
processResources {
    // 替换bootstrap.yaml文件中@project_version@占位符的值
    filesMatching('bootstrap.yaml') {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                project_name       : project.property('name'),
                project_version    : project.property('version'),
                project_description: project.property('description')
        ]
    }
    // 默认application文件中的配置从配置中心获取
    excludes = ["**/application.yaml", "**/application.properties"]
}

//打ar包
bootJar {
    enabled = true
    archiveFileName = "${rootProject.name}.${archiveExtension.get()}" //'sei-auth.jar'
    mainClassName = MainClass
    manifest {
        attributes("Manifest-Version": "6.0")
        attributes("Created-By": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString())
        attributes("Implementation-Title": "$project.rootProject.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Implementation-Vendor": "rcsit.cn")
        attributes("Description": "$project.description")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

jar {
    enabled = true
    exclude('bootstrap.*')
    exclude('application.*')
    exclude('com/changhong/sei/auth/AuthApplication.*')
    manifest {
        attributes("Manifest-Version": "6.0")
        attributes("Created-By": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString())
        attributes("Implementation-Title": "$project.rootProject.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Implementation-Vendor": "rcsit.cn")
        attributes("Description": "$project.description")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

/////////////////////////////////////上传Maven仓库////////////////////////////////////////
// 打包源代码
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// 如果希望gradle install，安装到.m2本地仓库，参考下面的内容
install {
    repositories.mavenInstaller {
        pom.version = "$project.version"
        pom.artifactId = "$project.name"
        pom.groupId = "$project.group"
    }
}

//上传到nexus
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO_URL) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            pom.version = "$project.version"
            pom.artifactId = "$project.name"
            pom.groupId = "$project.group"
        }
    }
    return 'Success'
}
